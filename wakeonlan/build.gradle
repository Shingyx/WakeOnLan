plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "de.mannodermaus.android-junit5" version "1.10.0.0"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (FileNotFoundException ignored) {
    logger.warn('keystore.properties not found')
}

android {
    namespace 'com.github.shingyx.wakeonlan'
    signingConfigs {
        release {
            storeFile rootProject.file(keystoreProperties['storeFile'] ?: "default.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdk 35
    defaultConfig {
        applicationId "com.github.shingyx.wakeonlan"
        minSdkVersion 26
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        viewBinding true
    }
    kotlin {
        jvmToolchain(21)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
}
